// Multiply 2 values
// Arguments:
// - value1
// - value2
// Returns multiplication result
math_mul_unsign:
push r12
mov r12, r14
push r1
push r2
push r3
push r4
// Get first parameter from the stack
// Set map of RAM addresses
ldi r0, #2
lsh r0, #28
// Or'ing stack frame with the map
or r0, r12
// First parameter will have an offset r12 + 3
ldi r3, #3
add r3, r0
// Load first parameter's value to r1 register
ld r1, r3
// Second parameter will have an offset r12 + 4
ldi r3, #4
add r3, r0
// Load second parameter's value to r2 register
ld r2, r3
// Clear return value
clr r0
cmp r1, r2
ldi r3, math_mul_unsign_lt_br
brltu r3
// greater than branch
mov r3, r1
mov r4, r2
ldi r1, math_mul_unsign_loop
br r1
// less than branch
math_mul_unsign_lt_br:
mov r3, r2
mov r4, r1
math_mul_unsign_loop:
// r3 - contains lesser value (will be a counter)
// r4 - contains greater value
clr r1
// if r3 is zero, return from function
cmp r3, r1
ldi r1, math_mul_unsign_ret
breq r1
ldi r2, #1
// Decrement a counter
sub r3, r2
// Add r4 to return value
add r0, r4
ldi r1, math_mul_unsign_loop
br r1
math_mul_unsign_ret:
// Return from function
pop r4
pop r3
pop r2
pop r1
pop r12
ret

// Divide value1 by value2
// Arguments:
// - Dividend
// - Divider
// - Returns integer part of division in r0 and division remainder in r1 register
math_div_unsign:
push r12
mov r12, r14
// values will be returned in r0 and r1 registers, so I'm not save their values to the stack
push r2
push r3
push r4
// Get parameters from the stack
// Set map of RAM addresses
ldi r0, #2
lsh r0, #28
// Or'ing stack frame with the map
or r0, r12
// First parameter will have an offset r12 + 3
ldi r4, #3
add r4, r0
// Load first parameter's value (dividend) to r2 register
ld r2, r4
// Second parameter will have an offset r12 + 4
ldi r4, #4
add r4, r0
// Load second parameter's value (divider) to r3 register
ld r3, r4
// Clear return values
clr r0
clr r1
// if value2 (r3 register) is zero, return from function (cannot divide by 0)
cmp r3, r1
ldi r4, math_div_unsign_ret
breq r4
math_div_unsign_loop:
cmp r2, r3
ldi r4, math_div_unsign_ret
// if value in r2 (dividend) less than value in r3 (divider), then branch
brltu r4
ldi r4, #1
add r0, r4
sub r2, r3
// If r2 and r3 are equal, then exit from function
ldi r4, math_div_unsign_ret
breq r4
ldi r4, math_div_unsign_loop
br r4
math_div_unsign_ret:
// move reminder to r1 register
mov r1, r2
// Return from function
pop r4
pop r3
pop r2
pop r12
ret

// Extends a sign bit of a short (16bit) value
// Returns sign extended value
// Arguments:
// - Value to extend a sign
math_short_sign_extend:
push r12
mov r12, r14
push r1
// Get parameter from the stack
// Set map of RAM addresses
ldi r1, #2
lsh r1, #28
// Or'ing stack frame with the map
or r1, r12
// Parameter will have an offset r12 + 3
ldi r0, #3
add r1, r0
// Load parameter's value to r0 register
ld r0, r1
// Extend 15'th bit of the value
mov r1, r0
rsh r1, #15
// rsh set zero bit if result is 0
ldi r1, math_short_sign_extend_one
brne r1
// extend 0
ldi r1, #0xffff
// Clrear bits 31-16
and r0, r1
ldi r1, math_short_sign_extend_ret
br r1
// extend 1
math_short_sign_extend_one:
ldi r1, #0xffff
lsh r1, #16
// Set bits 31-16
or r0, r1
// Return from function
math_short_sign_extend_ret:
pop r1
pop r12
ret

// Return a 2'th complement value of a parameter
// Arguments:
// - Value for getting 2'th complement 
math_get_2th_complement:
push r12
mov r12, r14
push r1
// Get parameter from the stack
// Set map of RAM addresses
ldi r1, #2
lsh r1, #28
// Or'ing stack frame with the map
or r1, r12
// Parameter will have an offset r12 + 3
ldi r0, #3
add r1, r0
// Load parameter's value to r0 register
ld r0, r1
// Calculate 2'th complement
ldi r1, #1
not r0
add r0, r1
// Return from function
pop r1
pop r12
ret

// Checks, does parameter negative
// Arguments:
// - Value to check
// Returns:
// 0 - if not negative
// 1 - if negative
math_is_long_negative:
push r12
mov r12, r14
push r1
// Get parameter from the stack
// Set map of RAM addresses
ldi r1, #2
lsh r1, #28
// Or'ing stack frame with the map
or r1, r12
// Parameter will have an offset r12 + 3
ldi r0, #3
add r1, r0
// Load parameter's value to r0 register
ld r0, r1
// Only 31'nd bit will remain
rsh r0, #31
// Return from function
pop r1
pop r12
ret
